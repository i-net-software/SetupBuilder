//*
plugins {
    id "de.inetsoftware.setupbuilder" version "3.4.3"
}
/*/
buildscript {
    
    repositories {
        maven {
            url uri('../../repo')
        }
    }
    dependencies {
        classpath 'de.inetsoftware:SetupBuilder:+'
    }
}

repositories {
    maven {
        url uri('../../repo')
    }
}

apply plugin: 'de.inetsoftware.setupbuilder'
//*/

apply plugin: 'java'

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
    }
}

setupBuilder {

    application = "Setup Builder Test"
    description = 'Build a test Programm.'
    icons = "test-icon.icns"

    licenseFile "license_en.txt"
    licenseFile {
       locale = "de"
       resource = "license_de.txt"
    }

    from jar.outputs

    bundleJre = 1.8
    mainClass = 'com.inet.testapplication.TestLauncher'
    mainJar = 'testBuilds.jar'

    runBeforeUninstall {
        mainClass = 'com.inet.testapplication.TestLauncher'
    }
    service {
        displayName = "i-net TestLaunch Server"
        id = "helpdesk"
    }
}

configure( [ deb, rpm ] ) {
    doFirst {
        setupBuilder {
            defaultResourceLanguage "de"
            longDescription "longDesc_de.txt"
            longDescription {
                locale = "en"
                resource = "longDesc_en.txt"
            }
        }
    }
}

deb {
    checkPackage = false
}

dmg {
    preferencesLink {
        title = "Testing Link"
        action = "echo Hello World"
    }
    preferencesLink {
        title = "Second Testing Link"
        action = "echo Another World, hello"
    }

    daemonUser = "testingUser"
}

dmg.dependsOn jar
deb.dependsOn jar
rpm.dependsOn jar
msi.dependsOn jar

// If the 'dmg' task is to be build, make sure that the pref pane exists as well
import org.apache.tools.ant.taskdefs.condition.Os
task preparePrefPane(type: GradleBuild) {

    buildFile '../../SetupBuilderOSXPrefPane/build.gradle'
    tasks = ['clean', 'uploadArchives']

    processResources.dependsOn 'preparePrefPane'

    onlyIf {
        file(buildFile).exists() && Os.isFamily(Os.FAMILY_MAC)
    }
}

// run the following task as a dependency to the "check"-task.
task runAppBundlerTest(type: GradleBuild) {

    buildFile = './appBundler.gradle'
    tasks = ['clean', 'bundleApp']

    processResources.dependsOn 'runAppBundlerTest'

    onlyIf {
        file(buildFile).exists() && Os.isFamily(Os.FAMILY_MAC)
    }
}

// run the following task as a dependency to the "check"-task.
task runJNLPBundlerTest(type: GradleBuild) {

    buildFile = './jnlpBundler.gradle'
    tasks = ['clean', 'bundleApp']

    processResources.dependsOn 'runJNLPBundlerTest'

    onlyIf {
        file(buildFile).exists() && Os.isFamily(Os.FAMILY_MAC)
    }
}
