plugins {
  id "com.gradle.plugin-publish" version "0.9.1"
}

apply plugin: 'java'
apply plugin: 'maven'

group = 'de.inetsoftware'
version = '1.9.0'
sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
def LIB_GRADLE_VERSION = '3.5'

println 'Gradle version: ' + gradle.gradleVersion

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src']
            exclude '**/*.java'
            exclude '**/package.html'
        }
    }
}

if( !System.getProperty("local") && file( '../BuildScripts/base.gradle' ).exists() ) {
    apply from: '../BuildScripts/base.gradle' // for internal build system
    preparePublish.dependsOn 'publishPluginJavaDocsJar'
    preparePublish.dependsOn 'jar'
    preparePublish.dependsOn 'publishPluginJar'
    println "Uploading into internal Repository 'fileserver'"

    if ( System.getProperty("snapshot") ) {
        version += '-SNAPSHOT' // setting version to snapshot
    }

    wrapper.gradleVersion = LIB_GRADLE_VERSION
} else {
    println "Uploading into local '../repo'"
    version += '-SNAPSHOT' // setting version to snapshot
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: uri('../repo'))
            }
        }
    }

    task wrapper(type: Wrapper) {
       gradleVersion = LIB_GRADLE_VERSION
    }
}

pluginBundle {
  website = 'https://github.com/i-net-software/SetupBuilder'
  vcsUrl = 'https://github.com/i-net-software/SetupBuilder'
  description = 'The Setup Builder is a plugin for Gradle which can create native setups for different platforms like Windows, Linux and OSX. The output is a *.msi, a *.deb, a *.rpm or a *.dmg file.'
  tags = ['setup', 'installer', 'msi', 'dmg', 'deb', 'rpm', 'windows', 'linux', 'osx' ]

  plugins {
    setupBuilderPlugin {
      id = 'de.inetsoftware.setupbuilder'
      displayName = 'Gradle Setup Builder plugin'
    }
    appBunderPlugin {
      id = 'de.inetsoftware.appbundler'
      displayName = 'Gradle Application Bundler plugin for OSX'
      tags = ['app']
    }
  }
}
